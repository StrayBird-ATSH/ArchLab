#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Wang, Chen
# 16307110064
#
# Describe how and why you modified the baseline code.
# The original baseline code is modified in great extension from the following 
# four perspectives:
# 1. The straightforward loop is unrolled into nine continuous executions.
# Therefore, if the data is longer than 9, each loop will do nine continuous
# copy actions, which can greatly reduce the number of iterations thus reducing 
# the condition control overhead.
# 2. There is usually the case that there are remainders that is not a multiple 
# of nine. In this case, I just made the copy execution in a straight line way
# without any iterations. This can reduce the control overhead for this part.
# 3. Each condition jump is placed and designed carefully to make the prediction 
# have the best correct possibilities.
# 4. For the remaining parts, avoiding data hazard is also paid attention to
# so as to elevate the efficiency.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
# Loop header
    iaddq $-8, %rdx         # len = len - 8
	jle Continue           # if len > 0, then go to Loop

########-----Loop Separator-------################################

Loop:
	mrmovq (%rdi), %rcx	    # save the first element from memory to register %rcx
	iaddq $72, %rdi		    # src = src + 9 (move 72 bytes, i.e. 8 words forward)
	iaddq $72, %rsi			# dst = dst + 9 (move 72 bytes, i.e. 8 words forward)
	mrmovq -64(%rdi), %r8	# save the second element from memory to register %r8
	rmmovq %rcx, -72(%rsi)  # save the value to the destination memory
	andq %rcx, %rcx		    # test %rcx
	jle LoopJmp1			# if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax		    # add the number of postive numbers

LoopJmp1:
	mrmovq -56(%rdi), %r9   # to avoid load/use hazard, this read memory instruction is advanced
	rmmovq %r8, -64(%rsi)	# save the second value to the destination memory
	andq %r8, %r8			# test for future reference
	mrmovq -48(%rdi), %r10  # to avoid load/use hazard, this read memory instruction is advanced
	jle LoopJmp2			# if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax			# add the number of postive numbers

LoopJmp2:
	rmmovq %r9, -56(%rsi)   # save the third value to the destination memory
	andq %r9, %r9           # test for future reference
	mrmovq -40(%rdi), %r11  # to avoid load/use hazard, this read memory instruction is advanced
	jle LoopJmp3            # if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp3:
	rmmovq %r10, -48(%rsi)  # save the forth value to the destination memory
	andq %r10, %r10         # test for future reference
	jle LoopJmp4            # if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp4:
	rmmovq %r11, -40(%rsi)  # save the fifth value to the destination memory
	mrmovq -32(%rdi), %r8   # to avoid load/use hazard, this read memory instruction is advanced
	andq %r11, %r11         # test to see if the value is no greater than 0
	jle LoopJmp5
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp5:
	rmmovq %r8, -32(%rsi)   # save the sixth value to the destination memory
	andq %r8, %r8           # test for future reference
	mrmovq -24(%rdi), %r9   # to avoid load/use hazard, this read memory instruction is advanced
	jle LoopJmp6            # if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp6:
	rmmovq %r9, -24(%rsi)   # save the seventh value to the destination memory
	mrmovq -16(%rdi), %r10  # to avoid load/use hazard, this read memory instruction is advanced
	andq %r9, %r9           # test for future reference
	jle LoopJmp7            # if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp7:
	rmmovq %r10, -16(%rsi)  # save the eighth value to the destination memory
	mrmovq -8(%rdi), %r11   # to avoid load/use hazard, this read memory instruction is advanced
	andq %r10, %r10         # test for future reference
	jle LoopJmp8            # if the value is no greater than 0, then do not increment %rax
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp8:
	rmmovq %r11, -8(%rsi)   # save the ninth value to the destination memory
	andq %r11, %r11         # test to see if the value is no greater than 0
	jle LoopJmp9
	iaddq $1, %rax          # add the number of postive numbers

LoopJmp9:
	iaddq $-9, %rdx			# len - 9 and then len > 0 ?
	jg Loop					# if so, the loop will continue

########-----Loop Separator-------################################

Continue:
    iaddq $5, %rdx
    mrmovq (%rdi), %rcx		# save the first element from memory to register %rcx
    jl LessThan3
    jg GreaterThan3
    jmp RemAdd3

LessThan3:
    iaddq $2, %rdx
    je RemAdd1
    jg RemAdd2
    ret

GreaterThan3:
    iaddq $-3, %rdx
    jl FourAndFive
    jg SevenAndEight
    jmp RemAdd6

FourAndFive:
    iaddq $2, %rdx
    je RemAdd4
    jmp RemAdd5

SevenAndEight:
    iaddq $-2, %rdx
    jl RemAdd7
    jmp RemAdd8

RemAdd8:
    mrmovq 56(%rdi), %r11	# save the second element from memory to register %r11
    rmmovq %r11, 56(%rsi)	# save the value to the destination memory
    andq %r11, %r11			# test *src to see whether the value is less than 0
    jle RemAdd7             # if not greater than 0, jump
    iaddq $1, %rax			# add the count of postive numbers

RemAdd7:
    mrmovq 48(%rdi), %r8	# save the third element from memory to register %r8
    rmmovq %r8, 48(%rsi)	# save the value to the destination memory
    andq %r8, %r8			# test *src to see whether the value is less than 0
    jle RemAdd6             # if not greater than 0, jump
    iaddq $1, %rax			# add the count of postive numbers

RemAdd6:
    mrmovq 40(%rdi), %r9	# save the forth element from memory to register %r9
    rmmovq %r9, 40(%rsi)	# save the value to the destination memory
    andq %r9, %r9			# test *src to see whether the value is less than 0
    jle RemAdd5             # if not greater than 0, jump
    iaddq $1, %rax			# add the count of postive numbers

RemAdd5:
    mrmovq 32(%rdi), %r10	# save the fifth element from memory to register %r10
    rmmovq %r10, 32(%rsi)	# save the value to the destination memory
    andq %r10, %r10         # test *src to see whether the value is less than 0
    jle RemAdd4             # if not greater than 0, jump
    iaddq $1, %rax          # add the count of postive numbers

RemAdd4:
    mrmovq 24(%rdi), %r11   # save the sixth element from memory to register %r11
    rmmovq %r11, 24(%rsi)	# save the value to the destination memory
    andq %r11, %r11         # test *src to see whether the value is less than 0
    jle RemAdd3             # if not greater than 0, jump
    iaddq $1, %rax          # add the count of postive numbers

RemAdd3:
    mrmovq 16(%rdi), %r8	# save the seventh element from memory to register %r8
    rmmovq %r8, 16(%rsi)	# save the value to the destination memory
    andq %r8, %r8           # test *src to see whether the value is less than 0
    jle RemAdd2             # if not greater than 0, jump
    iaddq $1, %rax          # add the count of postive numbers

RemAdd2:
    mrmovq 8(%rdi), %r9     # save the eighth element from memory to register %r9
    rmmovq %r9, 8(%rsi)     # save the value to the destination memory
    andq %r9, %r9           # test *src to see whether the value is less than 0
    jle RemAdd1             # if not greater than 0, jump
    iaddq $1, %rax          # add the count of postive numbers

RemAdd1:
    rmmovq %rcx, (%rsi)     # save the value to the destination memory
    andq %rcx, %rcx			# test *src to see whether the value is less than 0
    jle Done                # if not greater than 0, jump
    iaddq $1, %rax			# add the count of postive numbers
	ret

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
